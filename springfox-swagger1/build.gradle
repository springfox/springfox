plugins {
  id "net.ltgt.apt-idea" version "0.15"
}

ext {
  limits = [
      'instruction': 97,
      'branch'     : 95,
      'line'       : 97,
      'complexity' : 94,
      'method'     : 94,
      'class'      : 95.5
  ]
}

sourceSets {
  integrationTest { sourceSet ->
    ["java", "groovy", "scala", "resources"].each {
      if (!sourceSet.hasProperty(it)) return
      sourceSet."$it".srcDir file("src/integration-test/${it}")
    }
  }
}


// Define integration test task
task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
}

dependencies {
  compile libs.swagger2Core
  compile project(':springfox-spi')
  compile project(':springfox-schema')
  compile project(':springfox-spring-web')
  compile project(':springfox-swagger-common')
  compile libs.core
  compile libs.spring


  provided libs.springProvided
  provided libs.clientProvided

  compile "org.mapstruct:mapstruct:$mapstruct"
  apt "org.mapstruct:mapstruct-processor:$mapstruct"

  testCompile project(':springfox-core')
  testCompile project(':springfox-core').sourceSets.test.output
  testCompile project(':springfox-schema').sourceSets.test.output
  testCompile project(':springfox-swagger-common').sourceSets.test.output
  testCompile project(':springfox-spring-web').sourceSets.test.output
  testCompile libs.test

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
}

compileJava {
  options.compilerArgs += [
          "-Amapstruct.defaultComponentModel=spring",
          "-Amapstruct.unmappedTargetPolicy=ERROR"
          ]
}