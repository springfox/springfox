import springfox.gradlebuild.utils.ProjectDefinitions

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/idea.gradle"

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "com.github.adrianbk:gradle-jvmsrc-plugin:0.6.1"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1"
    classpath "com.github.adrianbk:gradle-travisci-trigger-plugin:1.0.0"
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    classpath "com.ofg:uptodate-gradle-plugin:1.5.2"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
    classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.2"
  }
}
apply plugin: 'build-dashboard'
apply plugin: 'project-report'
apply plugin: 'springfox-multi-release'

ext {
  apiKey = System.getenv('GIT_HUB_API_KEY')
  jdkVersion = 1.6
}

subprojects {
  apply plugin: "com.ofg.uptodate"
  //Not strictly groovy projects but useful for the IDE to recognise groovy test sources
  apply plugin: 'groovy' //gradle groovy plugin extends the java plugin
  apply plugin: 'com.github.adrianbk.jvmsrc'
  apply plugin: 'checkstyle'

  javadoc {
    options.encoding = 'UTF-8'
  }
  //Commented PMD and Findbugs as its taking too much time for very little value
  //Perhaps run every 20 CI builds (mod of ci build number)
//  apply from: "$rootDir/gradle/code-quality.gradle"
  if (ProjectDefinitions.publishable(it)) {
    apply from: "$rootDir/gradle/coverage.gradle"
    apply from: "$rootDir/gradle/publishing.gradle"
  }
  repositories {
    jcenter()
  }

  checkstyle {
    configFile = file("$rootDir/config/checkstyle.xml")
  }

  checkstyleMain.source = "src/main/java"

  test {
    maxParallelForks = 2
  }

  configurations {
    provided
    compile.extendsFrom provided
  }

  jvmsrc {
    packageName "springfox"
  }

  compileJava.options.encoding = 'UTF-8'

  sourceCompatibility = jdkVersion
  targetCompatibility = jdkVersion
  group = 'io.springfox'
  version = project.rootProject.version

  jar {
    manifest {
      attributes(
          'Implementation-Title': "${project.name}",
          'Implementation-Version': version.toString(),
          'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
          'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
          'Build-Time': "${new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")}",
          'Built-By': System.getProperty('user.name'),
          'Built-On': "${InetAddress.localHost.hostName}/${InetAddress.localHost.hostAddress}"
      )
    }
  }

}

apply from: "$rootDir/gradle/documentation.gradle"

task wrapper(type: Wrapper) {
  gradleVersion = '2.9'
}
