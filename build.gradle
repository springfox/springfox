import springfox.gradlebuild.version.BuildscriptVersionResolver
import springfox.gradlebuild.version.SemanticVersion
import springfox.gradlebuild.version.SnapshotVersion
import springfox.gradlebuild.version.SoftwareVersion

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/idea.gradle"

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "com.github.adrianbk:gradle-jvmsrc-plugin:0.6.1"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1"
    classpath "com.github.adrianbk:gradle-travisci-trigger-plugin:1.0.0"
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    classpath "com.ofg:uptodate-gradle-plugin:1.5.0"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1"
    classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.2"
  }
}
apply plugin: 'build-dashboard'
apply plugin: 'project-report'

ext {
  apiKey = System.getenv('GIT_HUB_API_KEY')
  jdkVersion = 1.6
}

apply plugin: 'springfox-multi-release'


SoftwareVersion currentVersion = BuildscriptVersionResolver.projectVersion(project,
        new SnapshotVersion(SemanticVersion.get(file("$rootDir/version.properties")))
)
version = currentVersion

configure(publishable(subprojects)) { subproject ->
  apply plugin: "com.ofg.uptodate"
  //Not strictly groovy projects but useful for the IDE to recognise groovy test sources
  apply plugin: 'groovy' //gradle groovy plugin extends the java plugin

  test {
    maxParallelForks = 2
  }

  ext {
    jdkVersion = 1.6
  }
  compileJava.options.encoding = 'UTF-8'

  sourceCompatibility = jdkVersion
  targetCompatibility = jdkVersion

  configurations {
    provided
    compile.extendsFrom provided
  }

  group = 'io.springfox'
  version = currentVersion

  apply plugin: 'com.github.adrianbk.jvmsrc'
  jvmsrc {
    packageName "springfox"
  }

  jar {
    manifest {
      attributes(
              'Implementation-Title': "${project.name}",
              'Implementation-Version': version.toString(),
              'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
              'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
              'Build-Time': "${new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")}",
              'Built-By': System.getProperty('user.name'),
              'Built-On': "${InetAddress.localHost.hostName}/${InetAddress.localHost.hostAddress}"
      )
    }
  }

  apply plugin: 'checkstyle'
  checkstyle {
    configFile = file("$rootDir/config/checkstyle.xml")
  }
  checkstyleMain.source = "src/main/java"

  apply plugin: 'maven-publish'
  apply from: "$rootDir/gradle/publishing.gradle"
  apply from: "$rootDir/gradle/artifacts.gradle"
  apply from: "$rootDir/gradle/bintray_publish.gradle"
  apply from: "$rootDir/gradle/coverage.gradle"
  //Commented PMD and Findbugs as its taking too much time for very little value
  //Perhaps run every 20 CI builds (mod of ci build number)
//  apply from: "$rootDir/gradle/code-quality.gradle"

  repositories {
    jcenter()
  }

}

def publishable(subprojects) {
  subprojects.findAll {
    !['swagger-contract-tests', 'buildSrc'].contains(it.name)
  }
}

apply plugin: "com.github.adrianbk.tcitrigger"

tciTrigger {
  gitHubRepo 'adrianbk/swagger-springmvc-demo'
  gitHubApiKeyVarName = 'GIT_HUB_API_KEY'
  travisVariable {
    name = "LATEST_SSMVC_VERSION"
    value = "${version}"
    visible = true
  }
}
apply from: "$rootDir/gradle/asciidoc.gradle"
