import org.apache.tools.ant.filters.ReplaceTokens

/*
 * Copyright 2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "de.undercouch.download" version "3.4.3"
  id "com.moowork.node" version "1.2.0"
}

ext {
  swaggerUiVersion = '3.23.11'
  swaggerUiDist = "build/libs/swagger-ui-dist.zip"
  swaggerUiExplodedDir = "swagger-ui-${swaggerUiVersion}/dist/"
  downloadUrl = "https://github.com/swagger-api/swagger-ui/archive/v${swaggerUiVersion}.zip"
  destinationZip = "$buildDir/zip/${swaggerUiVersion}.zip"
  replacePath = 'window.location.origin + "\\${pageContext.request.contextPath}/api-docs"'
  artifactRepoBase = 'http://oss.jfrog.org/artifactory'
}

dependencies {
  provided project(':springfox-spring-webmvc')
  testCompile libs.test
}

node {
  version = '8.12.0' // LTS
  npmVersion = '6.4.1'
  distBaseUrl = 'https://nodejs.org/dist'
  download = true
  workDir = file("${project.buildDir}/nodejs")
  npmWorkDir = file("${project.buildDir}/npm")
  nodeModulesDir = file("${project.projectDir}/src/web")
}

task swaggerUiDownload {
  outputs.file(destinationZip) 
  doLast {
    download {
      src downloadUrl
      dest destinationZip
      onlyIfNewer true
    }
  }
}

task unzip(type: Copy) {
  dependsOn swaggerUiDownload
  from zipTree(file(destinationZip))
  include "*/dist/**"
  into file("${buildDir}")
}

task npmTest(type: NpmTask) {
  args = ['run', 'test']
}

task npmBuild(type: NpmTask) {
  args = ['run', 'build']
}

task npmAuditFixForce(type: NpmTask) {
  args = ['audit', 'fix', '--force']
}

task npmAuditFix(type: NpmTask) {
  args = ['audit', 'fix']
}

task npmAudit(type: NpmTask) {
  args = ['audit']
}

task npmCi(type: NpmTask) {
  dependsOn npmSetup
  args = ["ci"]
  outputs.dir("node_modules")
}

task customizeSwaggerUi {
  doLast {
    copy {
      from("${projectDir}/src/web/dist/springfox.js")
      into("${buildDir}/${swaggerUiExplodedDir}")
    }
    copy {
      from("${projectDir}/src/web/dist/springfox.js.map")
      into("${buildDir}/${swaggerUiExplodedDir}")
    }
    copy {
      from("${projectDir}/src/web/css/springfox.css")
      into("${buildDir}/${swaggerUiExplodedDir}")
    }
    copy {
      from "${projectDir}/src/web/fonts"
      into "${buildDir}/${swaggerUiExplodedDir}/fonts"
    }
    copy {
      from "${projectDir}/src/web/swagger-ui.html"
      into "${buildDir}/swagger-ui-${swaggerUiVersion}"
      filter(ReplaceTokens, tokens: [VERSION: "${project.version}".toString()])
    }
    delete "${buildDir}/${swaggerUiExplodedDir}/index.html"
    delete "${buildDir}/${swaggerUiExplodedDir}/swagger-ui.js"
  }
}

jar {
  from("${buildDir}/swagger-ui-${swaggerUiVersion}/swagger-ui.html") {
    into "META-INF/resources"
  }

  from("${buildDir}/${swaggerUiExplodedDir}") {
    into "META-INF/resources/webjars/${project.name}"
  }

  manifest {
    attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": project.version,
        "SwaggerUi-Version": swaggerUiVersion,
    )
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task cleanup(type: Delete) {
  delete "${buildDir}/${swaggerUiExplodedDir}"
}

clean.dependsOn(cleanup)
if (project.hasProperty("local")) {
  npmTest.dependsOn(npm_install)
  npmBuild.dependsOn(npm_install, npmTest)
} else {
  npmTest.dependsOn(npmCi)
  npmBuild.dependsOn(npmCi, npmTest)
}

customizeSwaggerUi.dependsOn(unzip, npmBuild)
jar.dependsOn(customizeSwaggerUi)
